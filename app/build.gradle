/*
 * 	Copyright (c) 2017. Toshi Inc
 *
 * 	This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'witness'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    dataBinding.enabled = true

    defaultConfig {
        applicationId "org.toshi"
        minSdkVersion 21
        targetSdkVersion 27

        versionCode 39
        versionName "1.3.0"

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    configurations.all {
        exclude module: 'httpclient' // from com.github.WhisperSystems:libsignal-service-java
        exclude module: 'commons-logging' // from com.github.WhisperSystems:libsignal-service-java
        exclude module: 'json' // from com.vdurmont:emoji-java
    }

    def webDebugEnabled = 'WEB_DEBUG_ENABLED'

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            multiDexEnabled true
            debuggable true
            minifyEnabled false
            buildConfigField "boolean", webDebugEnabled, "true"
        }
        developer {
            applicationIdSuffix ".developer"
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", webDebugEnabled, "true"
        }
        release {
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", webDebugEnabled, "false"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    // Give each APK in the API split a different versionCode and versionName
    project.ext.versionCodes = ['armeabi-v7a': 2, 'arm64-v8a': 3, 'x86': 4]
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) * 10000000 + android.defaultConfig.versionCode
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

configurations {
    ktlint
}

def supportLibVersion = '27.1.0'
def retrofitVersion = '2.3.0'
def archVersion = '1.1.1'

dependencies {
    implementation(
            // Support
            "com.android.support:appcompat-v7:${supportLibVersion}",
            "com.android.support:recyclerview-v7:${supportLibVersion}",
            "com.android.support:gridlayout-v7:${supportLibVersion}",
            "com.android.support:design:${supportLibVersion}",
            "com.android.support:cardview-v7:${supportLibVersion}",
            'com.android.support.constraint:constraint-layout:1.0.2',
            'com.android.support:multidex:1.0.0',
            'com.google.android:flexbox:0.3.2',
            // Push notifications
            'com.google.firebase:firebase-messaging:12.0.0',
            // Networking
            "com.squareup.retrofit2:retrofit:${retrofitVersion}",
            "com.squareup.retrofit2:converter-moshi:${retrofitVersion}",
            "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}",
            "com.squareup.retrofit2:converter-scalars:${retrofitVersion}",
            'com.squareup.okhttp3:logging-interceptor:3.10.0',
            'com.artemzin.rxjava:proguard-rules:1.1.9.0',
            'com.github.bumptech.glide:glide:3.7.0',
            'com.github.bumptech.glide:okhttp3-integration:1.4.0@jar',
            // Reactive
            'io.reactivex:rxandroid:1.2.1',
            'com.jakewharton.rxbinding:rxbinding:1.0.0',
            // Images
            'de.hdodenhof:circleimageview:2.2.0',
            'com.makeramen:roundedimageview:2.3.0',
            // Reading barcodes
            'com.journeyapps:zxing-android-embedded:3.5.0',
            // Crypto
            'org.bitcoinj:bitcoinj-core:0.14.3',
            'com.github.WhisperSystems:libsignal-service-java:70a0b223b2',
            'com.madgag.spongycastle:core:1.54.0.0',
            'com.madgag.spongycastle:prov:1.54.0.0',
            //UI
            'com.beloo.widget:ChipsLayoutManager:0.3.7@aar',
            'com.github.toshiapp:cropiwa:v1.0.2',
            'com.github.toshiapp:ahbottomnavigation:v2.0.7',
            'com.vdurmont:emoji-java:3.3.0',
            //Kotlin
            "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version",
            //Architecture components
            "android.arch.lifecycle:extensions:$archVersion",
            'com.jakewharton.timber:timber:4.7.0',
            "com.crashlytics.sdk.android:crashlytics:2.9.1"
    )

    ktlint "com.github.shyiko:ktlint:0.12.1"

    testImplementation(
            'junit:junit:4.12',
            'org.hamcrest:hamcrest-library:1.3',
            'org.mockito:mockito-core:2.16.0'
    )
    androidTestImplementation(
            "com.android.support:support-annotations:${supportLibVersion}",
            'com.android.support.test:runner:0.5',
            'com.android.support.test:rules:0.5',
            'org.hamcrest:hamcrest-library:1.3'
    )
}

dependencyVerification {
    verify = [
        // TODO: Update this when https://github.com/signalapp/gradle-witness/issues/27 is fixed
    ]
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

apply plugin: 'com.google.gms.google-services'