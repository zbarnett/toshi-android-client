/*
 * 	Copyright (c) 2017. Toshi Inc
 *
 * 	This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'witness'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    dataBinding.enabled = true

    defaultConfig {
        applicationId "org.toshi"
        minSdkVersion 16
        targetSdkVersion 27

        versionCode 34
        versionName "1.2.1"

        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    configurations.all {
        exclude module: 'httpclient' // from com.github.WhisperSystems:libsignal-service-java
        exclude module: 'commons-logging' // from com.github.WhisperSystems:libsignal-service-java
        exclude module: 'json' // from com.vdurmont:emoji-java
    }

    def webDebugEnabled = 'WEB_DEBUG_ENABLED'

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            multiDexEnabled true
            debuggable true
            minifyEnabled false
            buildConfigField "boolean", webDebugEnabled, "true"
        }
        developer {
            applicationIdSuffix ".developer"
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", webDebugEnabled, "true"
        }
        release {
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", webDebugEnabled, "false"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    // Give each APK in the API split a different versionCode and versionName
    project.ext.versionCodes = ['armeabi-v7a': 2, 'arm64-v8a': 3, 'x86': 4]
    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) * 10000000 + android.defaultConfig.versionCode
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

configurations {
    ktlint
}

def supportLibVersion = '27.1.0'
def retrofitVersion = '2.3.0'
def archVersion = '1.1.0'

dependencies {
    implementation(
            // Support
            "com.android.support:appcompat-v7:${supportLibVersion}",
            "com.android.support:recyclerview-v7:${supportLibVersion}",
            "com.android.support:gridlayout-v7:${supportLibVersion}",
            "com.android.support:design:${supportLibVersion}",
            "com.android.support:cardview-v7:${supportLibVersion}",
            'com.android.support:multidex:1.0.0',
            'com.google.android:flexbox:0.2.6',
            // Push notifications
            'com.google.firebase:firebase-messaging:11.6.0',
            // Networking
            "com.squareup.retrofit2:retrofit:${retrofitVersion}",
            "com.squareup.retrofit2:converter-moshi:${retrofitVersion}",
            "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}",
            "com.squareup.retrofit2:converter-scalars:${retrofitVersion}",
            'com.squareup.okhttp3:logging-interceptor:3.5.0',
            'com.artemzin.rxjava:proguard-rules:1.1.9.0',
            'com.github.bumptech.glide:glide:3.7.0',
            'com.github.bumptech.glide:okhttp3-integration:1.4.0@jar',
            // Reactive
            'io.reactivex:rxandroid:1.2.1',
            'com.jakewharton.rxbinding:rxbinding:1.0.0',
            // Images
            'de.hdodenhof:circleimageview:2.1.0',
            'com.makeramen:roundedimageview:2.3.0',
            // Reading barcodes
            'com.journeyapps:zxing-android-embedded:3.5.0',
            // Crypto
            'org.bitcoinj:bitcoinj-core:0.14.3',
            'com.github.WhisperSystems:libsignal-service-java:70a0b223b2',
            'com.madgag.spongycastle:core:1.54.0.0',
            'com.madgag.spongycastle:prov:1.54.0.0',
            //UI
            'com.beloo.widget:ChipsLayoutManager:0.3.7@aar',
            'com.github.toshiapp:cropiwa:v1.0.2',
            'com.github.toshiapp:ahbottomnavigation:v2.0.7',
            'com.vdurmont:emoji-java:3.3.0',
            //Kotlin
            "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version",
            //Architecture components
            "android.arch.lifecycle:extensions:$archVersion",
            'com.jakewharton.timber:timber:4.6.1',
    )

    kapt 'com.android.databinding:compiler:2.3.1'
    ktlint "com.github.shyiko:ktlint:0.12.1"

    compile('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
    testImplementation(
            'junit:junit:4.12',
            'org.hamcrest:hamcrest-library:1.3',
            'org.mockito:mockito-core:2.16.0'
    )
    androidTestImplementation(
            "com.android.support:support-annotations:${supportLibVersion}",
            'com.android.support.test:runner:0.5',
            'com.android.support.test:rules:0.5',
            'org.hamcrest:hamcrest-library:1.3'
    )
}

dependencyVerification {
    verify = [
            'com.crashlytics.sdk.android:crashlytics:8fd52c8fd857e67fb015de4f7eaa2510223a19bb308c59d8264f08f7dd641372',
            'com.google.firebase:firebase-core:ad7954769f68912b17b2794c3346376d378f851a4a77345682d5b50be8e95588',
            'com.crashlytics.sdk.android:beta:5045a1dcfd4461ef398ea999a94d8f7fa31ab589d296cb849ea0b751e91d75ca',
            'com.crashlytics.sdk.android:crashlytics-core:8ea320d38abcb9d1aea7f2b9895781137925babb17c40a4ae9c298570670ec93',
            'com.crashlytics.sdk.android:answers:8ac7ec5ae3977f2e8e59889853f958d818b60f751973ea3b8108a8695a1de6c5',
            'io.fabric.sdk.android:fabric:2d2e8b920042bc59b7610639885314718b53ac3f3f9744587d04d0304880a557',
            'com.google.firebase:firebase-analytics:9a7e631ced30ceab9045482703b8ebe060f24a0f4d248e7584fa354d438700c2',
            'com.google.firebase:firebase-analytics-impl:30e7607128faad3a9e4e0cfe0e7677ec89299164382f77f79af9f0b6db125edd',
            'com.google.firebase:firebase-iid:7e026d50ed0e4af6dde2041791a14cae1f0214e71d1bda0b50ac27bf295a3789',
            'com.google.firebase:firebase-common:1087fa973146806d76b58c4099de09434af76598705543bd89e114a5aa5188f0',
            'com.google.android.gms:play-services-tasks:7170ce5450c460323076bdd2cacc8fefdcd1f00e3e127c8a9dd7e8bdc302ef3d',
            'com.google.android.gms:play-services-basement:92c9c5768d19f00b94617782288af1ee381c93777063f382a864b662ac031ba0',
            'com.google.firebase:firebase-analytics-license:8f0a811ff5e0f4bfdfe38a6191e78bcfecd82dd8be286d7697d31b8353e155d3',
            'com.android.support:support-v4:cd030f875dc7ee73b58e17598f368a2e12824fb3ceb4ed515ed815a47160228c',
            'com.google.android.gms:play-services-basement-license:a5eb1b1f56c51e94014ff0f955af35fe81d97e9a58426bcda2eedab879b73ad8',
            'com.google.firebase:firebase-common-license:e52362ba02569c2f3ad8ccc05ca87854dd8ff4f3afe9226c37905496d8ae9e26',
            'com.google.firebase:firebase-analytics-impl-license:eea62c319d999225e82bb41c622f761075940cc864401cd8d7bfac9e17b49ff1',
            'com.android.support:support-fragment:f12633dd4d418a4edeb5ecf3bf4393edd0770b1eaa5d1ee3078c5e7c174868fd',
            'com.android.support:support-media-compat:e9f820d08e6a5735cfdb2a7d81d3c86b4a31897ac1edaeb55c7de06bcb370343',
            'com.android.support:support-core-utils:e0561cc9d00ae125d9e1ad8985d4639e68ce8399ae973e91674e97faaf658243',
            'com.android.support:support-core-ui:cf3c75fd9a1b1dcbb6042d610515cd79cd0d65d3efd272d2250727187e8ca2ed',
            'com.android.support:support-compat:5a7b6e18903458e3a561df24033476518f998cd7ae1ed747c2874e0685b999c7',
            'com.google.android.gms:play-services-tasks-license:c7d1faa9af7c460ab7829f6c08770391ed8cff4bd16c15cc2e0c1999a5952b1a',
            'com.google.firebase:firebase-iid-license:5a9c31d1da9d0273f270ff595c75ba4b3d84675a629b9015bc23dbcdd8803110',
            'com.android.support:support-annotations:47a2a30eab487a490a8a8f16678007c3d2b6dcae1e09b0485a12bbf921200ec3',
    ]
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}

apply plugin: 'com.google.gms.google-services'